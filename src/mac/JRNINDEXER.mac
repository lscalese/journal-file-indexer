ROUTINE JRNINDEXER
#include %occStatus
#include %occMessages
#include journalindexer
JRNINDEXER()
    New
    #define LOWER(%a)           $zcvt(%a,"L")
    #Define ZLEFTALIGN(%1,%2)   $Reverse($Justify($Reverse(%1),%2))

    #dim JournalFiles As %DynamicArray
    #dim iter As %Iterator.Array
    Set sc = $$$OK, jrn="", detailView = $$$NO, %zFilter = {}

    Try {

        For  {
            Write #, ##class(%Utility).Center("Journal File Indexer")
            Write !, ##class(%Utility).Center("--------------------")
            
            Write !,!," 1) Show list of indexed journal files."
            Write !," 2) Navigate."
            Write !," 3) Search (list view)."
            Write !," 4) Search (detail view)."
            Write !," 5) Index a journal file in database."
            Write !," 6) Show stats."
            Write !," 7) Restore data."
            Write !,!, "(Q)uit or (#) Menu item => "

            Read *Item Set Item = $Char(Item)
        
            If $$$LOWER($Extract(Item)) = "q" Quit

            Do ProcessSelect
        }

    } Catch ex {
        Set sc = ex.AsStatus()
        Do $SYSTEM.Status.DisplayError(sc)
    }

    Return sc

ProcessSelect
    If +Item = 1 Do ListJournalIndexed Write !,!," Press <any key> to return to main menu." Read *x
    If +Item = 2 Set detailView = $$$YES Do ListJournalIndexed, Navigate
    If +Item = 3 Set detailView = $$$NO Do ShowRecords
    If +Item = 4 Set detailView = $$$YES Do ShowRecords
    If +Item = 5 Do ##class(dc.journalindexer.services.Indexer).RunIndex() Write !,##class(%Utility).Center("Press <any key>") Read *x
    If +Item = 6 Do SelectAndShowStats
    If +Item = 7 Do Restore
    Quit

ListJournalIndexed
    Kill jrn, i

    Set JournalFiles = ##class(dc.journalindexer.dao.JournalDataDAO).GetJournalFiles(.sc)
    
    If $$$ISERR(sc) Do $SYSTEM.Status.DisplayError(sc) Quit

    Set iter = JournalFiles.%GetIterator()
    
    Write #
    Write !,!, ##class(%Utility).Center("Indexed journal files")
    Write !, ##class(%Utility).Center("---------------------")

    While iter.%GetNext(.key, .journalInfo, .type) {
        Write !," ",$Justify($Increment(i), 3), ") ", journalInfo.Name, $Replace(" (%1)", "%1", journalInfo.UserDefinedName), $Replace(" [%1]", "%1", journalInfo.ID)
        Set jrn(i) = $ListBuild(journalInfo.ID,journalInfo.UserDefinedName,journalInfo.FirstRecord,journalInfo.LastRecord)
    }

    Quit

Navigate
    Write !,!, "Select a journal file # => " Read x
    Quit:x=""
    
    If '$Data(jrn($p(x," ")),FileInfo) Write !,"Item ", $p(x," "), " not found." Quit

    Set journalEntry = ##class(dc.journalindexer.data.Record).FileAddrOpen($LG(FileInfo,1),$LG(FileInfo,3)),x = "n"

    While $IsObject(journalEntry) {
        
        If 'journalEntry.%IsA("dc.journalindexer.data.SetKillRecord") {
            Set journalEntryD = {}
            For propertyName = "Address", "TypeName", "PrevAddress", "NextAddress", "TimeStamp", "InTransaction", "ProcessID", "RemoteSystemID", "ECPSystemID" {
                Do journalEntryD.%Set(propertyName, $Property(journalEntry, propertyName))
            }
            Set journalEntry = journalEntryD
        }
        
        Do DisplayDetailView
        
        Write !, "(P)revious (Q)uit (N)ext (or <any other key>)." 
        Read *x Set x = $$$LOWER($c(x)) Quit:x="q"
        
        Set goToAddr = +$Select(x="p": journalEntry.PrevAddress, 1: journalEntry.NextAddress)

        Continue:goToAddr=0
        Set journalEntry = ##class(dc.journalindexer.data.Record).FileAddrOpen($LG(FileInfo,1), goToAddr)
    }

    Set journalEntry = ""
    
    Quit

ShowRecords
    Do ListJournalIndexed

    Write !,!, "Select a journal file # => " Read x
    Quit:x=""

    If '$Data(jrn($p(x," ")),FileInfo) Write !,"Item ", $p(x," "), " not found." Quit

    Set outWizard = $$FilterWizard($ListGet(FileInfo,1))
    Set filter = $Select($IsObject(outWizard):outWizard,1:{})

    Set filter.File = {"Value" : ($ListGet(FileInfo,1))}

    #dim tResult As %SQL.StatementResult
    Set tResult = ##class(dc.journalindexer.dao.Queries).SearchRecordFunc(filter)
    
    Set cnt = 0

    Set titles = $ListToString($ListBuild("Address", "TimeStamp", "Type", "Transaction", "Global", "Database"), $Char(9))
    
    Write:'detailView # Do WriteTitles

    Kill colNames Merge colNames = tResult.%GetMetaData().columnIndex

    While tResult.%Next(.sc) {
        
        Kill journalEntry Set journalEntry = {}
        Set key = "" For  Set key = $Order(colNames(key), 1, d) Quit:key=""  Do journalEntry.%Set($LG(d,2), tResult.%Get($LG(d,2)))
        
        If detailView {
            Do DisplayDetailView 
            Write !, "Press <any key> to display the next record or <q> to stop." Read *x Quit:x=$a("q")
            Continue
        }

        Do DisplayListView

        If $Increment(cnt) #20 '= 0 Continue
        
        Write !, "Press <any key> to display next 20 records or (Q) to stop." Read *x Quit:x=$a("q")
        Write # Do WriteTitles
        
    }

    If x'=$a("q") Write !,"  End Of Data.  Press <any key>." Read *x Quit
    kill tResult
    Quit

WriteTitles
    Write !, titles, !
    For i=1:1:80 Write "-"
    Quit

DisplayListView
    Write !
    For columnName = "Address", "TimeStamp", "TypeName", "InTransaction", "GlobalNode", "DatabaseName" {
        Write journalEntry.%Get(columnName), $Char(9)
    }
    Quit

DisplayDetailView
    
    Write #
    For i=1:1:80 Write "-"
    Write !, $$$ZLEFTALIGN("File: "_$ListGet(FileInfo, 2),25)
    Write ##class(%Utility).Center("First Address: "_$ListGet(FileInfo,3),25)
    Write $Justify("Last Address: "_$ListGet(FileInfo,4),30), !
    For i=1:1:80 Write "-"
    Write !, $$$ZLEFTALIGN("Address:",20), journalEntry.Address
    Write ##class(%Utility).Center("Next: " _ journalEntry.NextAddress,25)
    Write $Justify("Previous: " _ journalEntry.PrevAddress,20)
    Write !, $$$ZLEFTALIGN("TimeStamp:",20), journalEntry.TimeStamp
    Write !, $$$ZLEFTALIGN("Type:",20), journalEntry.TypeName
    Write !, $$$ZLEFTALIGN("In Transaction: ",20), $Select(journalEntry.InTransaction:"YES",1:"NO")
    Write !, $$$ZLEFTALIGN("Process ID: ",20), journalEntry.ProcessID
    Write !, $$$ZLEFTALIGN("Database:",20) , journalEntry.DatabaseName
    Write !, $$$ZLEFTALIGN("Global Node:",20) , journalEntry.GlobalNode
    Write !,!, $$$ZLEFTALIGN("Nbr of values:",20) , journalEntry.NumberOfValues
    Write !, $$$ZLEFTALIGN("Old Value:",20), journalEntry.FormattedOldValue
    Write !, $$$ZLEFTALIGN("New Value:",20), journalEntry.FormattedNewValue
    Write !,!
    Quit

SelectAndShowStats()
    New
    Do ListJournalIndexed
    Write !,!, "Select a journal file # => " Read x
    Quit:x=""
    If '$Data(jrn($p(x," ")),FileInfo) Write !,"Item ", $p(x," "), " not found." Quit

    Do ShowStats("gbl")
    Set x = ""

    For  {
        Write !,!, " (G)lobal stats, (P)rocess ID stats, (D)atabase stats (Q)uit => "
        Set invalid = 1
        While invalid { 
            Read *x Set x = $EXTRACT($$$LOWER($Char(x)))
            If "pgdq" [ x Set invalid = 0
        }

        If x = "q" Quit
        If x = "g" Do ShowStats("gbl") Continue
        If x = "p" Do ShowStats("pid") Continue
        If x = "d" Do ShowStats("db") Continue
    }

    Quit

ShowStats(typeStats="gbl")
    New (FileInfo,jrn,typeStats)

    Set by = $Case(typeStats,"gbl":"Global","pid":"Process ID","db":"Database",:typeStats)

    Set title = "Top Size by " _by _ " " _ $Lg(FileInfo, 2) _ " [ " _ $Lg(FileInfo, 1) _ " ]"
    Set underline = ""
    For i=1:1:$l(title) Set underline = underline_"-"

    Write #,##class(%Utility).Center(title)
    Write !,##class(%Utility).Center(underline)
    
    Set tResult = ##class(dc.journalindexer.dao.Queries).StatsByFunc($Lg(FileInfo, 1), typeStats), cpt = 0

    
    While tResult.%Next(.sc) {

        Set tmp = tResult.%Get("Key")
        Write !, $Justify($Increment(cpt),3), ") ", by, " ", tmp
        Write !, "     Size: ", $Justify(tResult.%Get("Size"), 10), " Count: ",tResult.%Get("Hit")," ",$tr(tResult.%Get("TypeJSON"),"{}""","()")

        If cpt # 10 '= 0 Continue

        Write !,!, " (C)ontinue, (Q)uit => "
        Set invalid = 1
        While invalid { 
            Read *x Set x = $EXTRACT($$$LOWER($Char(x)))
            If "cq" [ x Set invalid = 0
        }

        If x = "c" Write #,##class(%Utility).Center(title), !,##class(%Utility).Center(underline) Continue
        If x = "q" Quit
             
    }
    
    Quit 
 
Restore
    Do ListJournalIndexed

    Write !,!, "Select a source indexed journal file to restore data # => " Read x
    Quit:x=""

    If '$Data(jrn($p(x," ")),FileInfo) Write !,"Item ", $p(x," "), " not found." Hang 1 Quit

    Do {
        Set filter = $$FilterWizard($lg(FileInfo,1))

        Write #,##class(%Utility).Center("Restore data from indexed journal.")
        Write !,##class(%Utility).Center("----------------------------------"),!,!
        

        Write:$IsObject($Get(filter)) " Filter : ", filter.%ToJSON(),!,!

        If '$IsObject(filter) Quit
        If filter.%IsDefined("GlobalName") Quit

        Write !,!, "! A filter to the GlobalName is mandatory for this operation!"
        Write !,##class(%Utility).Center("<press any key>") Read *x

    } While 1
    
    If '$IsObject(filter) Quit

    Write " Restore from Indexed journal file : ", $ListGet(FileInfo,2)," [",$ListGet(FileInfo,1),"]",!,!

    Write " Restore (N)ew value or (O)ld value ? (Default: Old value) => "

    Read x Set x = $$$LOWER($Extract(x)) If x = "" Set x = "o" Write "O"
    
    If x '= "o", x '= "n" Write !,"BAD VALUE" Hang 2 Quit
    Set restoreValue = $s(x="n":"newvalue",1:"oldvalue")

    Write !,!," ! Data are not directly restored to the original global to prevent override !"
    Write !," ! Please provide a non existing global name to reste data !"
    
    Write !,!," Restore redirect to global => "
    Read x if x = "" Write !,"BAD VALUE" Hang 2 Quit

    Set redirectGbl = x 
    Set:$Extract(redirectGbl)'="^" redirectGbl = "^"_redirectGbl

    Write !,!, "Test only (Y)es (N)o (simulate the restore without SET operation) (Default: No) => "
    Read testOnly Set testOnly = $$$LOWER($Extract(testOnly)) Set testOnly = $s(testOnly="y":1,1:0)

    Write !, "Verbose (Y)es (N)o (Write on current device all operations) (Default: No) => "
    Read verbose Set verbose = $$$LOWER($Extract(verbose)) Set verbose = $s(verbose="y":1,1:0)

    Write !,!, $$$FormatText(" The global %1 in indexed journal %2 will be restored in global %3",filter.GlobalName.Value,$ListGet(FileInfo,2),redirectGbl)
    Write !,!, "Confirm start restore (Y)es (N)o (Default: No) => "
    Read x Set x = $$$LOWER($e(x))

    If x'="y" Write !,"CANCEL ..." HANG 1 Quit
    Try {
        Set filter.File={"Value":($ListGet(FileInfo,1))}
        Set sc = $$RunJobRestore(filter,restoreValue,redirectGbl,testOnly,verbose)
        If $$$ISERR(sc) $$$ThrowStatus(sc)
    }Catch ex {
        Do $SYSTEM.Status.DisplayError(ex.AsStatus())
    }
    Write !,!,##class(%Utility).Center("<press any key>") Read *z Kill z
    Quit

FilterWizard(FileId="")
    Quit $$^JRNINDEXERFW(FileId)

RunJobRestore(filter,restoreValue,redirectGbl,testOnly,verbose)
    New (filter,restoreValue,redirectGbl,testOnly,verbose)
    Set resourceName = "JRNINDEXERRESTORE"_$JOB
    Do:$SYSTEM.Event.Defined(resourceName) $SYSTEM.Event.Clear(resourceName), $SYSTEM.Event.Delete(resourceName)
    Do $SYSTEM.Event.Create(resourceName)
    Write !,"Start restore ...",!
    Job ##class(dc.journalindexer.services.Data).RestoreGlobal(filter.%ToJSON(),restoreValue,redirectGbl,"",0,resourceName,testOnly,verbose)
    Set childJob = $ZChild, (nSet,nErr,prevSize) = 0
    For  {
        Set returnList = $SYSTEM.Event.WaitMsg(resourceName,1)
        Set returnState = $ListGet(returnList, 1)

        If returnState = -1 Set sc = $$$ERROR($$$GeneralError, "Ressource deleted.") Quit
        If returnState = 0, '##class(%SYS.ProcessQuery).%ExistsId(childJob) Set sc = $$$ERROR($$$GeneralError, "Restore process not found.") Quit 

        Set msg = $ListGet(returnList, 2)
        If msg '= "", $ListValid(msg) {
            Set type = $lg(msg,1) Set:type'="" cpt(type) = $Get(cpt(type),0) + 1
            If type = "END" {
                Set sc = $Lg(msg,2)
                Write !," Restore finished with status: ", $Select($$$ISOK(sc):"OK",1:$SYSTEM.Status.GetOneErrorText(sc))
                Quit
            } ElseIf (type = "MSG") {
                Write !, $Lg(msg,2),!
                Set prevSize = 0
                Continue
            }
            If 'verbose { ; display only the progression
                For i=1:1:prevSize Write $c(8,32,8)
                Set wMsg = "SET: " _ $j($Get(cpt("SET"),0),10) _ "    " _ "ERR: " _ $j($Get(cpt("ERR"),0),10)
                Write wMsg
                Set prevSize = $Length(wMsg)
            } Else {
                Write !,type," ", $Lg(msg,2), " = ", $Lg(msg,3)
            }
        }
    }
    Do $SYSTEM.Event.Clear(resourceName), $SYSTEM.Event.Delete(resourceName)
    Quit sc